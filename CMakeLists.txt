CMAKE_MINIMUM_REQUIRED(VERSION 2.8)

PROJECT ( PySidePackaging_1_2_2 )

INCLUDE ( ExternalProject )

SET ( SHIBOKEN_CMAKE_CACHE_ARGS_STRING "" )
SET ( PYSIDE_CMAKE_CACHE_ARGS_STRING "" )
SET ( TOOLS_CMAKE_CACHE_ARGS_STRING "" )

IF ( PSP_PYTHON_EXECUTABLE )
  LIST ( APPEND SHIBOKEN_CMAKE_CACHE_ARGS_STRING "-DPYTHON_EXECUTABLE:string=${PSP_PYTHON_EXECUTABLE}" )
ENDIF ()

IF ( PSP_PYTHON_LIBRARY )
  LIST ( APPEND SHIBOKEN_CMAKE_CACHE_ARGS_STRING "-DPYTHON_LIBRARY:string=${PSP_PYTHON_LIBRARY}" )
ENDIF ()

IF ( PSP_PYTHON_INCLUDE_DIR )
  LIST ( APPEND SHIBOKEN_CMAKE_CACHE_ARGS_STRING "-DPYTHON_INCLUDE_DIR:string=${PSP_PYTHON_INCLUDE_DIR}" )
ENDIF ()

IF ( PSP_QT_QMAKE_EXECUTABLE )
  LIST ( APPEND SHIBOKEN_CMAKE_CACHE_ARGS_STRING "-DQT_QMAKE_EXECUTABLE:string=${PSP_QT_QMAKE_EXECUTABLE}" )
ENDIF ()

IF ( PSP_CMAKE_INSTALL_PREFIX )
  # Shiboken
  LIST ( APPEND SHIBOKEN_CMAKE_CACHE_ARGS_STRING "-DCMAKE_INSTALL_PREFIX:string=${PSP_CMAKE_INSTALL_PREFIX}" )
  # PySide
  LIST ( APPEND PYSIDE_CMAKE_CACHE_ARGS_STRING "-DCMAKE_INSTALL_PREFIX:string=${PSP_CMAKE_INSTALL_PREFIX}" )
  LIST ( APPEND PYSIDE_CMAKE_CACHE_ARGS_STRING "-DCMAKE_PREFIX_PATH:string=${PSP_CMAKE_INSTALL_PREFIX}/lib/cmake/Shiboken-1.2.2" )
  # Tools
  LIST ( APPEND TOOLS_CMAKE_CACHE_ARGS_STRING "-DCMAKE_INSTALL_PREFIX:string=${PSP_CMAKE_INSTALL_PREFIX}" )
ENDIF ()

ExternalProject_Add ( shiboken_1_2_2
  URL
  file:///${CMAKE_SOURCE_DIR}/archive/shiboken-1.2.2.tar.bz2
  CMAKE_CACHE_ARGS
  "${SHIBOKEN_CMAKE_CACHE_ARGS_STRING}"
  )

ExternalProject_Add ( pyside_1_2_2
  URL
  file:///${CMAKE_SOURCE_DIR}/archive/pyside-qt4.8+1.2.2.tar.bz2
  DEPENDS
  shiboken_1_2_2
  CMAKE_CACHE_ARGS
  "${PYSIDE_CMAKE_CACHE_ARGS_STRING}"
  )

ExternalProject_Add ( tools_0_2_15
  URL
  file:///${CMAKE_SOURCE_DIR}/archive/Tools-0.2.15.tar.gz
  DEPENDS
  pyside_1_2_2
  CMAKE_CACHE_ARGS
  "${TOOLS_CMAKE_CACHE_ARGS_STRING}"
  )
